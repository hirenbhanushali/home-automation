#include <ESP8266WiFi.h>
#include <Adafruit_MQTT.h>
#include <Adafruit_MQTT_Client.h>
#include <Servo.h>

Servo servo;

#define WIFI_SSID "Project"
#define WIFI_PASS "123456789"

#define MQTT_SERV "io.adafruit.com"
#define MQTT_PORT 1883
#define MQTT_NAME "cyloren"
#define MQTT_PASS "e6213827cb0849dcb5a0c4252e607e67"

//Set up MQTT and WiFi clients
WiFiClient client;
Adafruit_MQTT_Client mqtt(&client, MQTT_SERV, MQTT_PORT, MQTT_NAME, MQTT_PASS);

//Set up the feed you're subscribing to
Adafruit_MQTT_Subscribe cyloren = Adafruit_MQTT_Subscribe(&mqtt, MQTT_NAME "/f/cyloren");
Adafruit_MQTT_Subscribe AC = Adafruit_MQTT_Subscribe(&mqtt, MQTT_NAME "/f/AC");
Adafruit_MQTT_Subscribe extra = Adafruit_MQTT_Subscribe(&mqtt, MQTT_NAME "/f/extra");
Adafruit_MQTT_Subscribe Security = Adafruit_MQTT_Subscribe(&mqtt, MQTT_NAME "/f/Security");

#define light D1
#define ac D2
#define door D4

void setup()
{
  Serial.begin(9600);

  //Connect to WiFi
  Serial.print("\n\nConnecting Wifi... ");
  WiFi.begin(WIFI_SSID, WIFI_PASS);
  while (WiFi.status() != WL_CONNECTED)
  {
    delay(500);
  }

  Serial.println("OK!");

  //Subscribe to the cyloren feed
  mqtt.subscribe(&cyloren);
  mqtt.subscribe(&AC);
  mqtt.subscribe(&extra);
  mqtt.subscribe(&Security);

  pinMode(light, OUTPUT);
  digitalWrite(light, LOW);

  pinMode(ac, OUTPUT);
  digitalWrite(ac, LOW);

  servo.attach(door);
  servo.write(0);
}

void loop()
{
  MQTT_connect();
  
  //Read from our subscription queue until we run out, or
  //wait up to 5 seconds for subscription to update
  Adafruit_MQTT_Subscribe * subscription;
  while ((subscription = mqtt.readSubscription(5000)))
  {
    //If we're in here, a subscription updated...
    if (subscription == &cyloren)
    {
      //Print the new value to the serial monitor
      Serial.print("cyloren: ");
      Serial.println((char*) cyloren.lastread);
      
      //If the new value is  "ON", turn the light on.
      //Otherwise, turn it off.
      if (!strcmp((char*) cyloren.lastread, "ON"))
      {
        //Active low logic
        digitalWrite(light, HIGH);
      }
      else if(!strcmp((char*) cyloren.lastread, "OFF"))
      {
        digitalWrite(light, LOW);
      }
    }

    if (subscription == &AC)
    {
      Serial.print("AC: ");
      Serial.println((char*) AC.lastread);
      
      //If the new value is  "ON", turn the light on.
      //Otherwise, turn it off.
      if (!strcmp((char*) AC.lastread, "ON"))
      {
        //Active low logic
        digitalWrite(ac, HIGH);
      }
      else if(!strcmp((char*) AC.lastread, "OFF"))
      {
        digitalWrite(ac, LOW);
      }
    }

    if (subscription == &extra)
    {
      Serial.print("temp: ");
      Serial.println((char*) extra.lastread);
    }

    if (subscription == &Security)
    {
      Serial.print("Door: ");
      Serial.println((char*) Security.lastread);

      if (!strcmp((char*) Security.lastread, "ON"))
      {
        servo.write(90);
        delay(5000);
        servo.write(0);
        delay(1000);
      }
    }
  }

  // ping the server to keep the mqtt connection alive
  if (!mqtt.ping())
  {
    mqtt.disconnect();
  }
}


/***************************************************
  Adafruit MQTT Library ESP8266 Example

  Must use ESP8266 Arduino from:
    https://github.com/esp8266/Arduino

  Works great with Adafruit's Huzzah ESP board & Feather
  ----> https://www.adafruit.com/product/2471
  ----> https://www.adafruit.com/products/2821

  Adafruit invests time and resources providing this open source code,
  please support Adafruit and open-source hardware by purchasing
  products from Adafruit!

  Written by Tony DiCola for Adafruit Industries.
  MIT license, all text above must be included in any redistribution
 ****************************************************/

void MQTT_connect() 
{
  int8_t ret;

  // Stop if already connected.
  if (mqtt.connected()) 
  {
    return;
  }

  Serial.print("Connecting to MQTT... ");

  uint8_t retries = 3;
  while ((ret = mqtt.connect()) != 0) // connect will return 0 for connected
  { 
       Serial.println(mqtt.connectErrorString(ret));
       Serial.println("Retrying MQTT connection in 5 seconds...");
       mqtt.disconnect();
       delay(5000);  // wait 5 seconds
       retries--;
       if (retries == 0) 
       {
         // basically die and wait for WDT to reset me
         while (1);
       }
  }
  Serial.println("MQTT Connected!");
}
